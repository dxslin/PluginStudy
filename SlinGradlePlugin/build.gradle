import com.slin.study.buildsrc.rename.RenameTask
import com.slin.study.buildsrc.rename.IncrementalReverseTask


plugins {
    id 'java'
    // maven发布插件，用于将插件打包发布上传到仓库
    id 'maven-publish'
    id 'groovy'
    id 'kotlin'
}

// project组，引用插件时用到
group = "com.slin.study.gradle.plugin"
// 版本
version = "1.0.0"

// 上传仓库配置
uploadArchives {
    repositories {
        // 配置成本地仓库
        ivy { url "${rootDir.path}/plugin_release" }
    }
}

// 配置Java版本号
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    // 插件相关的API
    implementation gradleApi()
    // 测试相关的api
    testImplementation gradleTestKit()
    // 如果使用Groovy编写的，引入相应api
    implementation localGroovy()
    // 安卓项目结构的API，与根目录build.gradle文件中dependencies引入的安卓build插件一致。
    implementation "com.android.tools.build:gradle:4.2.2"
}



//下面为直接在build.gradle文件中创建Task示例
abstract class GreetingToFileTask extends DefaultTask {

    @OutputFile
    abstract RegularFileProperty getDestination()

    @TaskAction
    def greet() {
        def file = getDestination().get().asFile
        file.parentFile.mkdirs()
        file.write 'Hello!'
    }
}

def greetingFile = objects.fileProperty()

tasks.create('greet', GreetingToFileTask) {
    destination = greetingFile
}

tasks.create('sayGreeting') {
    dependsOn greet
    doLast {
        def file = greetingFile.get().asFile
        println "${file.text} (file: ${file.name})"
    }
}

greetingFile.set(layout.buildDirectory.file('hello.txt'))

tasks.register("rename", RenameTask){
    dependsOn greet

    inputApk = layout.buildDirectory.file('hello.txt')
    rule = "new_hello.txt"
    outDir = file("$buildDir/outputs")
}

tasks.register('incrementalReverse', IncrementalReverseTask) {
    inputDir = file('src')
    outputDir = file("$buildDir/outputs")
    inputProperty = project.properties['taskInputProperty'] ?: 'original'
    inputFile = file(".gitignore")
}